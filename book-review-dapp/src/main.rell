module;

entity book {
    key isbn: text;
    title: text;
    author: text;
}

entity book_review {
    index book: book;
    reviewer_name: text;
    review: text;
    rating: integer;
}

struct book_review_struct {
    book: struct<book>;
    reviewer_name: text;
    review: text;
    rating: integer;
}

operation create_book(isbn: text, title: text, author: text) {
    create book (isbn = isbn, title = title, author = author);
}

query get_all_books(){
    return book @* {} (.isbn, .title, .author);
}

operation create_book_review(isbn: text, reviewer_name: text, review: text, rating: integer) {
    val book = require( book @? { .isbn == isbn }, "book with isbn %s not found".format(isbn));
    create book_review(book, reviewer_name, review, rating);
}

query get_book_reviews(isbn: text) {
    require( book @? { .isbn == isbn } , "book with isbn %s not found".format(isbn) );
    return book_review @* { .book.isbn == isbn } (
        book_review_struct(
            book = .book.to_struct(),
            .reviewer_name,
            .review,
            .rating
        )
    );
}